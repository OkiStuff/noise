plugins {
  id "org.spongepowered.gradle.sponge.dev" version "1.1.1"
  id "net.kyori.indra.publishing.sonatype" version "2.0.6"
  id "net.saliman.cobertura" version "4.0.0"
  id "com.github.kt3k.coveralls" version "2.12.0"
}

group 'org.spongepowered'
version '2.0.0-SNAPSHOT'
description 'Noise generation library for Java, based on the libnoise C++ library.'

dependencies {
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

sourceSets {
  main {
    multirelease {
      alternateVersions(9)
      moduleName "org.spongepowered.noise"
    }
  }
}

javadoc {
  source("src/main/java9")
  options {
    addStringOption("-patch-module", "org.spongepowered.noise=${sourceSets.main.allJava.srcDirs.collect { it.absolutePath }.join(File.pathSeparator)}")
  }
}

afterEvaluate {
  javadoc {
    classpath += sourceSets.java9.output
    options {
      addStringOption("-release", "9")
    }
  }
}

cobertura.coverageFormats = ['html', 'xml']

processResources {
  from(rootProject.rootDir) {
    include 'LICENSE.txt'
  }
}

spongeConvention {
  repository("noise") {
    ci(true)
    publishing(true)
  }
  mitLicense()
}

indra {
  configurePublications {
    pom {
      developers {
        developer {
          id = 'DDoS'
          name = 'Aleksi Sapon'
          email = 'qctechs@gmail.com'
        }
        developer {
          id = 'kitskub'
          name = 'Jack Huey'
          email = 'kitskub@gmail.com'
        }
        developer {
          id = 'Wolf480pl'
          name = 'Wolf480pl'
          email = 'wolf480@interia.pl'
        }
        developer {
          id = 'lukespragg'
          name = 'Luke Spragg'
          email = 'the@wulf.im'
        }
      }
    }
  }
}
