plugins {
  alias libs.plugins.spongeConvention
  alias libs.plugins.spotless
  alias libs.plugins.errorprone
  alias libs.plugins.indra.sonatype
  alias libs.plugins.indra
  alias libs.plugins.indra.crossdoc
  id 'jacoco'
}

group 'org.spongepowered'
version '2.0.0-SNAPSHOT'
description 'Noise generation library for Java, based on the libnoise C++ library.'

configurations {
  tagletPath
}

dependencies {
  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine
  errorprone libs.errorprone
  errorprone libs.errorprone.annotations
  tagletPath project(':noise-doclet-extras')
}

sourceSets {
  main {
    multirelease {
      alternateVersions 9
      moduleName "org.spongepowered.noise"
      requireAllPackagesExported()
      applyToJavadoc(true)
    }
  }
}

check {
    dependsOn 'validateModule'
}

tasks.withType(JavaCompile).configureEach {
  options.errorprone.disable("InvalidBlockTag")
}

def taglets = configurations.named('tagletPath').map { it.incoming.files }
javadoc {
  inputs.files(taglets).withPropertyName("tagletPath")
  options.taglets("noise.build.SourceModulesTaglet")

  doFirst {
    options.tagletPath(taglets.get().files.toList())
  }
}

indraCrossdoc {
    baseUrl providers.gradleProperty('javadocPublishRoot')
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

processResources {
  from(rootProject.rootDir) {
    include 'LICENSE.txt'
    def projectName = project.name
    rename { "LICENSE_${projectName}.txt" }
  }
}

spongeConvention {
  repository("noise") {
    ci(true)
    publishing(true)
  }
  mitLicense()
}

spotless {
    java {
        endWithNewline()
	indentWithSpaces(4)
	trimTrailingWhitespace()
	removeUnusedImports()
	formatAnnotations()
    }
}

indra {
  javaVersions().minimumToolchain 17
  configurePublications {
    pom {
      developers {
        developer {
          id = 'DDoS'
          name = 'Aleksi Sapon'
          email = 'qctechs@gmail.com'
        }
        developer {
          id = 'kitskub'
          name = 'Jack Huey'
          email = 'kitskub@gmail.com'
        }
        developer {
          id = 'Wolf480pl'
          name = 'Wolf480pl'
          email = 'wolf480@interia.pl'
        }
        developer {
          id = 'lukespragg'
          name = 'Luke Spragg'
          email = 'the@wulf.im'
        }
      }
    }
  }
}
